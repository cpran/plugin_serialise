# Procedures to extract current preference values from the Praat
# preferences file and restore them at a later point.
#
# This script is part of the serialise CPrAN plugin for Praat.
# The latest version is available through CPrAN or at
# <http://cpran.net/plugins/serialise>
#
# The serialise plugin is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation, either
# version 3 of the License, or (at your option) any later version.
#
# The serialise plugin is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with serialise. If not, see <http://www.gnu.org/licenses/>.
#
# Copyright 2014, 2015 Jose Joaquin Atria

# Remember the value that was originally in the preferences file
#
procedure saveOutputPreferences ()
  .file$ = if unix      then "prefs5"           else
    ...    if windows   then "Preferences5.ini" else
    ...    if macintosh then "Prefs5"           else
    ...    "--undefined--" fi fi fi
  .file$ = preferencesDirectory$ + "/" + .file$

  .preferences$ = nocheck readFile$(.file$)
  .output$ = extractLine$(.preferences$, "TextEncoding.outputEncoding: ")
endproc

# Restore a remembered value to the preferences file
#
procedure restoreOutputPreferences ()
  if checkOutputPreferences.restore
    Text writing preferences: if saveOutputPreferences.output$ == "" then
      ... "try ASCII, then UTF-16" else
      ... saveOutputPreferences.output$
      ... fi
  endif
endproc

# Check the preferences file to see if a key has a particular value
# If the value is different, offer the user to change it temporarily or
# permanently.
#
procedure checkOutputPreferences ()
  @saveOutputPreferences()
  .restore = 0
  if saveOutputPreferences.output$ != "UTF-8"

    if saveOutputPreferences.output$ != ""
      @saveSelection()
      .tmp = Create Sound as pure tone: "test",
        ... 1, 0, 0.1, 44100, 220, 0.2, 0.01, 0.01
      .gui = nocheck View & Edit
      removeObject: .tmp
      @restoreSelection()
      if .gui = undefined
        .restore = 1
      else
        beginPause: "This script requires output to be set to UTF-8"
        .button = endPause: "Abort", "Set temporarily", "Set permanently", 3, 1
        if .button == 1
          exit
        elsif .button == 2
          .restore = 1
        elsif .button == 3
          .preferences$ = replace_regex$(saveOutputPreferences.preferences$,
            ... "^(TextEncoding.outputEncoding: ).*" ,"\1UTF-8", 1)
          writeFile: saveOutputPreferences.file$, .preferences$
        endif
      endif
    endif
    Text writing preferences: "UTF-8"
  endif
endproc
